#preparation - run once 
{
   ######## Adjustable values #########
   :local StartingAddress 100.64.0.1
   :local ClientCount 253
   ####################################
   
   # All client chain jump
   /ip firewall nat add chain=srcnat action=jump jump-target=clients src-address="$StartingAddress-$($StartingAddress + $ClientCount - 1)" 
} 

#on-up
{
  ######## Adjustable values #########
  :local PublicAddresses {1.1.1.1, 2.2.2.2, 3.3.3.3}
  :local startport 1025
  :local numberOfPorts 2000
  ####################################
  
  :local success false
  :local currentport
  :local foundport
  :local foundaddress
  
  
  #check for available block
  :if ($"remote-address" in 100.64.0.0/10) do={
    :foreach PublicAddress in=$PublicAddresses do={
      :if (!$success) do={
        :set currentport $startport
        :while (($currentport <= (65535 - $numberOfPorts)) && !$success) do={
          :if ([:len [/ip firewall nat find where to-addresses=$PublicAddress to-ports="$currentport-$($currentport + $numberOfPorts)"]] = 0) do={
            :set success true
            :set foundport $currentport
            :set foundaddress $PublicAddress
          } else={
            :set currentport ($currentport + $numberOfPorts)
          }
        }
      }
    }
  
    # Specific client chain jumps
    /ip firewall nat add chain=clients action=jump jump-target="$user" src-address=$"remote-address" comment=$user
    
    # Translation rules
    /ip firewall nat add chain="$user" action=src-nat protocol=tcp src-address=$"remote-address" to-addresses=$foundaddress to-ports="$foundport-$($foundport + $numberOfPorts)" comment=$user
    /ip firewall nat add chain="$user" action=src-nat protocol=udp src-address=$"remote-address" to-addresses=$foundaddress to-ports="$foundport-$($foundport + $numberOfPorts)" comment=$user
    /ip firewall nat add chain="$user" action=src-nat protocol=icmp src-address=$"remote-address" to-addresses=$foundaddress comment=$user
  }
}

#on down
{
  :if ($"remote-address" in 100.64.0.0/10) do={
    /ip firewall nat remove [find where src-address=$"remote-address"]
  }
}
